
plugins {
    id 'java'
    id 'application'
    id 'org.springframework.boot' version '3.0.6'
    id 'io.spring.dependency-management' version '1.1.0'
    id "com.github.node-gradle.node" version "5.0.0"
}

group = 'com.example'
version = '0.0.1-SNAPSHOT'
sourceCompatibility = '17'

repositories {
    mavenCentral()
    maven { url "https://oss.sonatype.org/content/repositories/snapshots/" }
}

node {
    // Version of node to use.
    version = '16.15.1'

    // Base URL for fetching node distributions (change if you have a mirror).
//    distBaseUrl = 'https://nodejs.org/dist'

    // If true, it will download node using above parameters.
    // If false, it will try to use globally installed node.
//    download = true

    // Set the work directory for unpacking node
    workDir = file("${project.projectDir}/src/main/frontend/nodejs")

    // Set the work directory for NPM
    npmWorkDir = file("${project.projectDir}/src/main/frontend/npm")

    // Set the work directory where node_modules should be located
    nodeModulesDir = file("${project.projectDir}/src/main/frontend")
}

tasks.register('npmInstallDependencies', NpmTask) {
    dependsOn('npmSetup')
    execOverrides {
        it.ignoreExitValue = true
        it.workingDir = 'src/main/frontend'
    }
    args = ['install']
}

tasks.register('npmBuild', NpmTask) {
    dependsOn('npmInstallDependencies')
    execOverrides {
//        it.ignoreExitValue = true
        it.workingDir = 'src/main/frontend'
    }
    args = ['run', 'build']
}

tasks.register('copyFrontendToBuild', Copy) {
    dependsOn('npmBuild')
    from "$projectDir/src/main/frontend/build/"
//    include "*"
    into "$buildDir/resources/main/static"
}

processResources{
    dependsOn 'copyFrontendToBuild'
}

dependencies {
    implementation 'org.springframework.boot:spring-boot-starter-web'
    implementation files('python')
    implementation 'org.springframework.boot:spring-boot-devtools'
    implementation 'com.graphql-java-kickstart:graphql-spring-boot-starter:13.0.0'
    implementation 'org.springframework.boot:spring-boot-starter-graphql'
    implementation 'org.springframework.boot:spring-boot-starter-data-jpa'
    implementation 'com.h2database:h2'

    testImplementation 'org.springframework.boot:spring-boot-starter-test'
//    testImplementation 'com.graphql-java-kickstart:graphql-spring-boot-starter-test:13.0.0'
}

tasks.named('test') {
    useJUnitPlatform()
}

//mainClassName='ca.myapp.Application'